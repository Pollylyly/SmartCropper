plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace="me.pqpo.smartcropperlib"
    ndkVersion '25.1.8937393'
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 10
        versionName "2.1.4"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
                abiFilters 'armeabi-v7a', "arm64-v8a", 'x86', 'x86_64'
                arguments "-DANDROID_TOOLCHAIN=clang", "-DANDROID_STL=c++_static"
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection Aligned16KB
    api 'org.tensorflow:tensorflow-lite:2.17.0'
}

publishing {
    publications {
        // 遍历所有的构建变体 (比如 release, debug)
        android.libraryVariants.all { variant ->
            // 为每个变体创建一个 MavenPublication
            create(variant.name, MavenPublication) {
                // groupId, artifactId, 和 version 会由 JitPack 自动提供
                // JitPack 会根据 variant 的名字自动选择正确的发布
                // 我们只需要告诉它发布的内容是什么

                // from components.release 的替代方案：
                // 直接指定这个变体打包后的 .aar 文件
                artifact(variant.packageLibraryProvider.get())
            }
        }
//        release(MavenPublication) {
//            from components.release
//            groupId = 'com.github.pqpo'
//            artifactId = 'smartcropperlib'
//            version = '2.1.4'
//        }
    }
//    repositories {
//        maven {
//            url "file://${buildDir}/repo"
//        }
//    }
}