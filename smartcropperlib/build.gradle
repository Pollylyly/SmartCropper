plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace="me.pqpo.smartcropperlib"
    ndkVersion '25.1.8937393'
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 10
        versionName "2.1.4"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
                abiFilters 'armeabi-v7a', "arm64-v8a", 'x86', 'x86_64'
                arguments "-DANDROID_TOOLCHAIN=clang", "-DANDROID_STL=c++_static"
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection Aligned16KB
    api 'org.tensorflow:tensorflow-lite:2.17.0'
}

publishing {
    publications {
        // 遍历所有的构建变体 (比如 release, debug)
        android.libraryVariants.all { variant ->
            // 为每个变体创建一个 MavenPublication
            create(variant.name, MavenPublication) {
                // 从 Gradle project 的属性中获取 GAV
                groupId = project.group
                // artifactId 使用根项目的名称
                artifactId = rootProject.name
                version = project.version

                //指定这个变体打包后的 .aar 文件
                artifact(variant.packageLibraryProvider.get())
            }
        }
    }
}